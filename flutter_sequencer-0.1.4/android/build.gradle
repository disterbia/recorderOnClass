group 'com.michaeljperri.flutter_sequencer'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 16

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    externalNativeBuild {
        cmake {
            version "3.10.2"
            path "CMakeLists.txt"
        }
    }
}

void cloneThirdPartyRepo(String name, String uri, String branch) {
    def dirPath = "$projectDir/third_party/$name"
    def dir = new File(dirPath)

    if (!dir.exists()) {
        dir.mkdirs()
        org.ajoberstar.grgit.Grgit.clone(
            dir: dirPath,
            uri: uri,
            refToCheckout: branch
        )
    }
}

task cloneThirdPartyRepos {
    cloneThirdPartyRepo('AudioKit', 'https://github.com/AudioKit/AudioKit', 'v4-master')
    cloneThirdPartyRepo('libnyquist', 'https://github.com/mikeperri/libnyquist.git', 'fix-wavpack-int-lossy')
    cloneThirdPartyRepo('oboe', 'https://github.com/google/oboe', '1.4-stable')
    cloneThirdPartyRepo('TinySoundFont', 'https://github.com/schellingb/TinySoundFont.git', 'master')
}

preBuild.dependsOn cloneThirdPartyRepos

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
